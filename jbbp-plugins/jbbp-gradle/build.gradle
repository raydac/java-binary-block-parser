def getProp(name, dflt) {
    if (project.hasProperty(name)) {
        return project.getProperty(name)
    } else {
        logger.warn('Can not find property "' + name + '" is not found')
        return dflt
    }
}

def jbbpVersion = getProp('jbbp_plugin_version', '1.3.1-SNAPSHOT')
def metaLibVersion = getProp('meta_lib_version', '1.1.2')

group = 'com.igormaznitsa'
version = jbbpVersion

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: "com.gradle.plugin-publish"

sourceCompatibility = 1.6
targetCompatibility = 1.6

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile "commons-io:commons-io:2.5"
    compile "com.igormaznitsa:jbbp:" + jbbpVersion
    compile "com.igormaznitsa:meta-annotations:" + metaLibVersion
    compile "com.igormaznitsa:meta-utils:" + metaLibVersion

    testCompile 'junit:junit:4.12'
}

repositories {
    mavenCentral()
    mavenLocal()
}

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.7"
    }
}

sourceSets {
    main {
        java {
            srcDirs 'src/main/java'
            srcDirs '../jbbp-plugin-common/src/main/java'
        }
    }
}

pluginBundle {
    website = 'https://github.com/raydac/java-binary-block-parser'
    vcsUrl = 'https://github.com/raydac/java-binary-block-parser'
    description = 'JBBP script translator!'
    tags = ['jbbp', 'converter']

    plugins {
        JBBPPlugin {
            id = 'com.igormaznitsa.gradle.jbbp'
            displayName = 'JBBP Sources Generator plugin'
        }
    }
}